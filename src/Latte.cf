-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

FunDef.	   FunDef ::= Type Ident "(" [Arg] ")" Block ;

FnDef.     TopDef ::= FunDef ;

StDef.     TopDef ::= "class" Type "{" [Att] "}" ;

separator nonempty TopDef "" ;

Arg. 	   Arg ::= Type Ident ;

separator  Arg "," ;

Att.     Att ::= Type Ident ";" ;

Meth.    Att ::= FunDef ;

separator Att "" ;

NewArr.       New ::= "new" Type "[" Expr "]" ;

NewSt.        New ::= "new" Type ;       

AIdent.       Assignable ::= Ident ;

AArr.         Assignable ::= Ident "[" Expr "]" ;

AMeth.        Assignable ::= Ident "(" [Expr] ")" ;

AList.        Assign ::= [Assignable] ;

ANewDot.       Assign ::= "(" New ")" "." [Assignable] ;

ANewArr.      Assign ::= "(" New ")" "[" Expr "]" ;

separator nonempty Assignable "." ;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Assign "=" Expr  ";" ;

Incr.      Stmt ::= Assign "++"  ";" ;

Decr.      Stmt ::= Assign "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

For.	   Stmt ::= "for" "(" Type Ident ":" Expr6 ")" Stmt ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

Array.	   Type ::= Type "[]";

Clas.     Type ::= Ident;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr6 ::= Assign ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

ECast.     Expr6 ::= "(" Type ")null" ;

ENew.      Expr6 ::= New ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

